Dux {
  initialState {
    cells: [] // Array of live cell coordinates [{x, y}]
    generation: 0
    bounds: { width: 60, height: 60 }
    isRunning: false
    speed: 5 // 1-10 steps per second
    mode: 'classic' // 'classic' | 'lifeGarden'
    birthCandidates: [] // Valid birth positions in Life Garden mode
  }

  slice: "grid"

  actions {
    setGrid(payload: { cells, bounds? })
    stepGrid()
    clearGrid()
    resetGrid(payload: { bounds? })
    toggleRunning()
    setCells(payload: { cells })
    loadPattern(payload: { pattern })
    loadPatternFromLibrary(payload: { name })
    setSpeed(payload: { speed })
    setMode(payload: { mode })
    applyPlayerBirth(payload: { cell })
  }

  selectors {
    getGrid: state => state[slice]
    getCells: state => state[slice].cells
    getGeneration: state => state[slice].generation
    getBounds: state => state[slice].bounds
    getIsRunning: state => state[slice].isRunning
    getSpeed: state => state[slice].speed
    getMode: state => state[slice].mode
    getBirthCandidates: state => state[slice].birthCandidates
  }

  requirements [
    "Given an initial state, should have empty cells and generation 0"
    "Given setGrid action with cells, should update cells in state"
    "Given stepGrid action, should increment generation"
    "Given clearGrid action, should set cells to empty array"
    "Given resetGrid action, should reset generation to 0 and optionally update bounds"
    "Given toggleRunning action, should toggle isRunning boolean"
    "Given setCells action, should update cells without changing generation"
    "Given loadPattern action with JSON, should deserialize and load pattern into state"
    "Given loadPatternFromLibrary action with pattern name, should load that pattern from library"
  ]
}
